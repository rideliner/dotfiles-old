#!/usr/bin/env zsh

function {
  if [[ $# -eq 0 ]]; then
    echo "Modules need to be set in ~/.dot"
    echo
    echo "Format:"
    echo "  zstyle ':ride' modules <mods>..."
    echo "ex. zstyle ':ride' modules tmux terminal rvm ruby"
    echo
    echo "Available modules:"

    local mods
    findAllModules mods

    column =(echo "${(@F)mods}")
  else
    local mod file
    local majorRow="${(l:$COLUMNS::=:)}" minorRow="${(l:$COLUMNS::-:)}"

    for mod (${(s:,:)*//=/}); do
      file="$DOTFILES_PATH/$mod/.help"

      echo
      echo "$majorRow"
      echo "${(l:$(( ($COLUMNS - $#mod) / 2 )):: :)}$mod"
      echo "$minorRow"
      echo

      if [[ -d $file:h ]]; then
        if [[ -f $file ]]; then
          source $file
        else
          warning "no help file found"
        fi
      else
        fail "not a module"
      fi
    done
  fi
} $*

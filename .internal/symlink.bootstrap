#!/usr/bin/env zsh

function {
  local mod mods src dst link
  local overwrite backup skip action
  local overwrite_all=false backup_all=false skip_all=false
  local origLink overLink

  getModulesAndDependencies mods
  loadMeta mods

  for src ($DOTFILES_PATH/${^mods}/*.symlink(N)); do
    mod=${src:h:t}
    link=${src:t:r}

    zstyle -T ":ride:symlink:$mod" $link
    origLink=$?
    zstyle -T ":ride:symlink:override:$mod" $link
    overLink=$?

    if [ $overLink -eq 1 ]; then
      zstyle -s ":ride:symlink:override:$mod" $link dst
    elif [ $origLink -eq 1 ]; then
      zstyle -s ":ride:symlink:$mod" $link dst
    else
      dst="$HOME/.$link"
    fi

    dst=$dst:a

    if [[ -f "$dst" || -d "$dst" || -L "$dst" ]]; then
      overwrite=
      backup=
      skip=

      if [[ "${dst:A}" == "$src" ]]; then
        skip="true"
      elif [[ "$overwrite_all" == "false" && "$backup_all" == "false" && "$skip_all" == "false" ]]; then
        action=

        user "File already exists: ${dst:t}, what do you want to do? [s]kip, [S]kip all, [o]verwrite, [O]verwrite all, [b]ackup, [B]ackup all?"
        while [[ "$action" == "" ]]; do
          read -s -k 1 "action"

          case "$action" in
            o )
              overwrite="true";;
            O )
              overwrite_all="true";;
            b )
              backup="true";;
            B )
              backup_all="true";;
            s )
              skip="true";;
            S )
              skip_all="true";;
            * )
              action="";;
          esac
        done
      fi

      overwrite=${overwrite:-$overwrite_all}
      backup=${backup:-$backup_all}
      skip=${skip:-$skip_all}

      if [[ "$skip" == "true" ]]; then
        success "skipped ${src#$DOTFILES_PATH/}"
      elif [[ "$backup" == "true" ]]; then
        mv "$dst" "${dst}.backup"
        status $? "moved $dst to ${dst}.backup"
      elif [[ "$overwrite" == "true" ]]; then
        rm -rf "$dst"
        status $? "removed $dst"
      fi
    fi

    if [[ "$skip" != "true" ]]; then  # "false" or empty
      ln -s "$src" "$dst"
      status $? "linked ${src#$DOTFILES_PATH/} to $dst"
    fi
  done
}

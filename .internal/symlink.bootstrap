#!/usr/bin/env zsh

source "${0:A:h}/dependency.zsh"
source "$DOTFILES_PATH/.internal/resolve.zsh"

function {
  local mod mods src dst link dir name invalid
  local overwrite backup skip action
  local overwrite_all=false backup_all=false skip_all=false
  local -Ua parts

  dotfiles/dependencies/getModulesResolved mods

  for src ($DOTFILES_PATH/${^mods}/*.symlink(N) $DOTFILES_PATH/${^mods}/*.symlink@*(N)); do
    # skip this symlink if generated for a different user
    if [[ "${src:t:e}" == "symlink@*" && "${${src:t:e}#symlink@}" != "$USER" ]]; then
      continue
    fi

    mod=${src:h:t}
    link=${src:t:r}

    style=":ride:${mod}:symlink:${link}"

    if ! dotfiles/check/isActive "$style"; then
      continue
    fi

    zstyle -s "$style" path dir
    zstyle -s "$style" name name

    dir=$(dotfiles/resolve/symlink $dir $link $mod)
    name=$(dotfiles/resolve/symlink $name $link $mod)

    dst="$dir/$name"
    dst=$dst:a

    action=
    overwrite=
    backup=
    skip=

    if [[ ! -e "${dst:h}" ]]; then
      # base directory doesn't exist
      dotfiles/log/user "Directory doesn't exist: ${dst:h}, do you want to create it? [Y/N]"
      while [[ "$action" == "" ]]; do
        read -s -k 1 "action"

        case "$action" in
          n | N )
            dotfiles/log/fail "skipped ${dst} because you chose not to create the directory for it."
            action='skip'
            ;;
          y | Y )
            mkdir -p "${dst:h}"
            ;;
          * )
            action="";;
        esac
      done

      if [[ "$action" == 'skip' ]]; then
        continue;
      fi
    elif [[ ! -d "${dst:h}" ]]; then
      # base "directory" isn't a directory
      dotfiles/log/fail "${dst:h} exists, but is not a directory. Cannot create a symlink at $dst."
      return $?
    elif [[ -f "$dst" || -d "$dst" || -L "$dst" ]]; then
      invalid=($dst(N-@))
      if [[ $#invalid -ne 0 ]]; then
        rm $dst
        dotfiles/log/success "removed invalid link $dst"
      elif [[ "${dst:A}" == "$src" ]]; then
        skip="true"
      elif [[ "$overwrite_all" == "false" && "$backup_all" == "false" && "$skip_all" == "false" ]]; then
        action=

        dotfiles/log/user "File already exists: ${dst}, what do you want to do? [s]kip, [S]kip all, [o]verwrite, [O]verwrite all, [b]ackup, [B]ackup all?"
        while [[ "$action" == "" ]]; do
          read -s -k 1 "action"

          case "$action" in
            o )
              overwrite="true";;
            O )
              overwrite_all="true";;
            b )
              backup="true";;
            B )
              backup_all="true";;
            s )
              skip="true";;
            S )
              skip_all="true";;
            * )
              action="";;
          esac
        done
      fi

      overwrite=${overwrite:-$overwrite_all}
      backup=${backup:-$backup_all}
      skip=${skip:-$skip_all}

      if [[ "$skip" == "true" ]]; then
        dotfiles/log/success "skipped ${src#$DOTFILES_PATH/}"
      elif [[ "$backup" == "true" ]]; then
        mv "$dst" "${dst}.backup"
        dotfiles/log/status $? "moved $dst to ${dst}.backup"
      elif [[ "$overwrite" == "true" ]]; then
        rm -rf "$dst"
        dotfiles/log/status $? "removed $dst"
      fi
    fi

    if [[ "$skip" != "true" ]]; then  # "false" or empty
      ln -s "$src" "$dst"
      dotfiles/log/status $? "linked ${src#$DOTFILES_PATH/} to $dst"
    fi
  done
}

#!/usr/bin/env zsh

function {
  local str file cert
  local action overwrite_all=false skip_all=false overwrite skip
  zstyle -a ':ride:config:ssl' certificates certs

  if [[ $#certs -eq 0 ]]; then
    warning 'no certificates set to be loaded'
  fi

  if [[ ! -s "$HOME/.pki/nssdb" ]]; then
    fail "no ssl db found at $HOME/.pki/nssdb"
    return $?
  fi

  for cert in $certs; do
    skip=
    overwrite=

    file="$DOTFILES_PATH/ssl/${cert}.pem"
    if [[ ! -s "$file" ]]; then
      fail "could not find $file"

      continue
    fi

    str="$(cat $file)"
    existing="$(certutil -L -d sql:$HOME/.pki/nssdb -n "${cert}" -a 2>/dev/null | sed 's/\r$//')"

    if [[ $? == 0 ]]; then
      if [[ "$str" == "$existing" ]]; then
        skip="true"
      elif [[ "$skip_all" == "false" && "$overwrite_all" == "false" ]]; then
        action=

        user "Certificate already exists: $cert, what do you want to do? [s]kip, [S]kip all, [o]verwrite, [O]verwrite all?"
        while [[ "$action" == "" ]]; do
          read -s -k 1 "action"

          case "$action" in
            o )
              overwrite="true";;
            O )
              overwrite_all="true";;
            s )
              skip="true";;
            S )
              skip_all="true";;
            * )
              action="";;
          esac
        done
      fi
    fi

    overwrite=${overwrite:-$overwrite_all}
    skip=${skip:-$skip_all}

    if [[ "$skip" == "true" ]]; then
      success "skipped $file"
    else
      certutil -d sql:$HOME/.pki/nssdb -A -t TC -n "${cert}" -i "$file"
      if [[ "$overwrite" == "true" ]]; then
        status $? "overwrote '$cert' ssl certificate"
      else
        status $? "added '$cert' ssl certificate"
      fi
    fi
  done
}

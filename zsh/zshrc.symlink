
[[ -s "$HOME/.local-rc" ]] && source "$HOME/.local-rc"

REAL_PATH=${${$(readlink -fnq -- ${ZDOTDIR-~}/.zshrc)%/*}%/*}

[[ -s "$REAL_PATH/default.path" ]] && source "$REAL_PATH/default.path"

typeset -U config_files
config_files=(${REAL_PATH}/**/*.zsh)

unset REAL_PATH

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete tools
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

[[ -s "$HOME/.local+rc" ]] && source "$HOME/.local+rc"

# remove missing path directories
path=($^path(N))

export path

ls


[[ -s "$HOME/.dot.conf" ]] && source "$HOME/.dot.conf"
[[ -s "$HOME/.dot.pre" ]] && source "$HOME/.dot.pre"

source $(echo ${ZDOTDIR:-$HOME}/.zshrc(:A:h:h))/.internal/path.zsh

[[ -s "$DOTFILES_PATH/.internal/default.path" ]] && source "$DOTFILES_PATH/.internal/default.path"

source "$DOTFILES_PATH/.internal/dependency.zsh"

local mods
getModulesAndDependencies mods

local -aU config_files path_files completion_files

# load the path files
for module in $mods; do
  zstyle -a ":ride:order:$module" path path_files
  path_files=($DOTFILES_PATH/$module/${^path_files}.path(N))
  path_files+=($DOTFILES_PATH/$module/*.path(N))

  for src_file in $path_files; do
    source $src_file
  done
done

# load the zsh files
for module in $mods; do
  zstyle -a ":ride:order:$module" zsh config_files
  config_files=($DOTFILES_PATH/$module/${^config_files}.zsh(N))
  config_files+=($DOTFILES_PATH/$module/*.zsh(N))

  for src_file in $config_files; do
    source $src_file
  done
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion file after autocomplete tools
for module in $mods; do
  zstyle -a ":ride:order:$module" completion completion_files
  completion_files=($DOTFILES_PATH/$module/${^completion_files}.completion(N))
  completion_files+=($DOTFILES_PATH/$module/*.completion(N))

  for src_file in $completion_files; do
    source $src_file
  done
done

unset mods module file config_files path_files completion_files

[[ -s "$HOME/.dot.post" ]] && source "$HOME/.dot.post"

# remove missing path directories
path=($^path(N))

export path

